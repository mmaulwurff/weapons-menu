#library "LIBWEAPONMENU"
#include "zcommon.acs"

#include "constants.acs"

#include "data.acs"
#include "favorite.acs"
#include "inventory_control.acs"
#include "string.acs"
#include "utils.acs"
#include "zscript-access.acs"
#include "settings.acs"

function int  getGlobalState(void) { return ScriptCall("wm_StaticModel", "getState"); }
function void setGlobalState(int state) { ScriptCall("wm_StaticModel", "setState", state); }

// Global variables ////////////////////////////////////////////////////////////
// To write to global variables, use only special functions.

// cached variables. Their values don't change during the game.
int  currentSet;

int  highlightedWeapon;
int  selectedWeapon;

bool selectedWatchEnabled = true;

bool isInInventory[MAX_WEAPONS_NUMBER];

// Inventory variables section /////////////////////////////////////////////////

int nInventoryItems;

str inventoryClasses[N_INVENTORY_ITEMS];
str inventoryNames  [N_INVENTORY_ITEMS];

// Get status section //////////////////////////////////////////////////////////

function bool IsOpened(void)    { return getGlobalState() == STATE_OPENED; }
function bool IsInventory(void) { return getGlobalState() == STATE_INVENTORY; }
function bool IsVisible(void)   { return getGlobalState() == STATE_OPENED || getGlobalState() == STATE_CLOSED_VISIBLE; }

// Global state must be changed only with this function.
function void WM_ChangeState(int newState, int playerN)
{
  if (newState == getGlobalState()) return;

  WM_LeaveState(getGlobalState());

  setGlobalState(newState);

  WM_EnterState(newState, playerN);
}

function void WM_LeaveState(int oldState)
{
  // leave previous state
  switch (oldState)
  {
  case STATE_CLOSED:         break;
  case STATE_OPENED:         WM_LeaveOpened();    break;
  case STATE_INVENTORY:      WM_LeaveInventory(); break;
  case STATE_CLOSED_VISIBLE: break;
  }
}

function void WM_EnterState(int newState, int playerN)
{
  switch (newState)
  {
  case STATE_CLOSED:         WM_EnterClosed(playerN);    break;
  case STATE_OPENED:         WM_EnterOpened();           break;
  case STATE_INVENTORY:      WM_EnterInventory(playerN); break;
  case STATE_CLOSED_VISIBLE: WM_EnterClosedVisible();    break;
  }
}

function void WM_LeaveOpened(void)
{
  WM_ClearScreen();
}

function void WM_LeaveInventory(void)
{
  WM_PlaySwitchSound();
  WM_ClearScreen();
}

function void WM_EnterClosed(int playerN)
{
  if (highlightedWeapon != selectedWeapon)
  {
    WM_SelectWeapon(highlightedWeapon, playerN);
  }

  if (IsAlwaysShowing())
  {
    WM_ChangeState(STATE_CLOSED_VISIBLE, playerN);
  }
}

function void WM_EnterOpened(void)
{
  WM_UpdateIsInInventory();
  WM_UpdateMenu();
}

function void WM_EnterInventory(int playerN)
{
  WM_SetHighlightedWeapon(WM_FindRealSelectedWeapon());
  WM_PlaySwitchSound();
  WM_PrintInventoryMenu(playerN);
}

function void WM_EnterClosedVisible(void)
{
  WM_UpdateIsInInventory();
  WM_UpdateMenu();
}

function void WM_MaybeLoadCustomWeaponOrder(int playerN)
{
  int previousSet = GetUserCVar(playerN, "m8f_wm_PreviousWeaponSet");
  if (previousSet != currentSet) return;

  int previousCount = GetUserCVar(playerN, "M8fWeaponMenuWeaponCount");
  if (previousCount != weaponCounts[currentSet]) return;

  if (currentSet == SET_FALLBACK)
  {
    str  currentPlayerClass  = GetActorClass(0);
    str  previousPlayerClass = GetUserCVarString(playerN, "m8f_wm_PreviousPlayerClass");
    bool sameClass           = (StrCmp(previousPlayerClass, currentPlayerClass) == 0);
    if (!sameClass) return;

    bool allWeaponsExist = WM_CheckCVarWeaponsExisting(playerN);
    if (!allWeaponsExist) return;
  }

  WM_LoadDataFromCVars(playerN);
}

function void WM_LoadDataFromCVars(int playerN)
{
  for (int i = 0; i < weaponCounts[currentSet]; ++i)
  {
    str weaponClassVarName = StrParam(s:"M8fWeaponMenuWeaponClass", i:i);
    str weaponNameVarName  = StrParam(s:"M8fWeaponMenuWeaponName",  i:i);
    str weaponSlotVarName  = StrParam(s:"M8fWeaponMenuWeaponSlot",  i:i);

    names[currentSet][i] = WM_Unwrap(GetUserCVarString(playerN, weaponClassVarName));
    tags [currentSet][i] = WM_Unwrap(GetUserCVarString(playerN, weaponNameVarName));
    slots[currentSet][i] = GetUserCVar(playerN, weaponSlotVarName);
  }
}

function bool WM_CheckCVarWeaponsExisting(int playerN)
{
  for (int i = 0; i < weaponCounts[currentSet]; ++i)
  {
    str weaponClassCVar = StrParam(s:"M8fWeaponMenuWeaponClass", i:i);
    str weaponClass     = WM_Unwrap(GetUserCVarString(playerN, weaponClassCVar));
    if (!CheckClass(weaponClass)) return false;
  }
  return true;
}

// Weapon data access functions ////////////////////////////////////////////////

function int WM_GetWeaponSlot(int weaponNumber)
{
  return slots[currentSet][weaponNumber];
}

function str WM_GetWeaponClass(int weaponNumber)
{
  return names[currentSet][weaponNumber];
}

function str WM_GetPrintableWeaponName(int weaponNumber)
{
  return tags[currentSet][weaponNumber];
}

// Returns weaponNumber of next weapon in this slot, or ERROR_NOT_FOUND if there
// is no more weapons in this slot.
function int WM_GetWeaponSibling(int weaponNumber, bool doCycle)
{
  int thisWeaponSlot = WM_GetWeaponSlot(weaponNumber);
  int weaponCount    = WM_WeaponCount();

  for (int i = 0; i < weaponCount; ++i)
  {
    int w = (weaponNumber + i) % weaponCount;
    if (w != weaponNumber
        && WM_GetWeaponSlot(w) == thisWeaponSlot
        && !WM_SkipNoAmmo(w))
    {
      if (!doCycle && w < weaponNumber) return ERROR_NOT_FOUND;
      return w;
    }
  }

  return ERROR_NOT_FOUND;
}

function int WM_GetWeaponBySlotInInv(int slotNumber)
{
  int weaponCount = WM_WeaponCount();

  for (int i = 0; i < weaponCount; ++i)
  {
    if (slotNumber != WM_GetWeaponSlot(i)) { continue; }

    str weaponClass = WM_GetWeaponClass(i);
    if (!CheckClass(weaponClass))     { continue; }
    if (!CheckInventory(weaponClass)) { continue; }
    if (WM_SkipNoAmmo(i))             { continue; }

    return i;
  }

  return ERROR_NOT_FOUND;
}

function bool WM_IsOnlyInSlot(int weaponNumber)
{
  int slot = WM_GetWeaponSlot(weaponNumber);

  for (int prev = weaponNumber - 1; prev >= 0; --prev)
  {
    if (WM_GetWeaponSlot(prev) != slot) { continue; }
    str prevWeaponClass = WM_GetWeaponClass(prev);
    if (!CheckClass(prevWeaponClass))     { continue; }
    if (!CheckInventory(prevWeaponClass)) { continue; }

    return false;
  }

  int weaponCount = WM_WeaponCount();
  for (int next = weaponNumber + 1; next < weaponCount; ++next)
  {
    if (WM_GetWeaponSlot(next) != slot) { continue; }
    str nextWeaponClass = WM_GetWeaponClass(next);
    if (!CheckClass(nextWeaponClass))     { continue; }
    if (!CheckInventory(nextWeaponClass)) { continue; }

    return false;
  }

  return true;
}

// Printing Weapons Menu functions /////////////////////////////////////////////

function void WM_UpdateMenu(void)
{
  WM_ClearScreen();
  if (IsVisible()) { WM_PrintMenu(); }
}

function void WM_ClearScreen(void)
{
  int id  = msgIdWeapon;
  int end = msgIdWeapon;
  int weaponCount = WM_WeaponCount();
  if (nInventoryItems > weaponCount) { end += nInventoryItems; }
  else                               { end += weaponCount;     }

  do // when menu is disabled, weapon count is 0, so always clear first message.
  {
    HudMessage(s:""; msgFlags, id, 0, 0, 0, 0);
    ++id;
  } while (id < end);

  // clear slots numbers
  end = msgIdWeapon + MAX_WEAPONS_NUMBER + weaponCount;
  for (id = msgIdWeapon + MAX_WEAPONS_NUMBER; id < end; ++id)
  {
    HudMessage(s:""; msgFlags, id, 0, 0, 0, 0);
  }

  HudMessage(s:""; msgFlags, msgIdSelected,    0, 0, 0, 0);
  HudMessage(s:""; msgFlags, msgIdHighlighted, 0, 0, 0, 0);
  HudMessage(s:""; msgFlags, msgIdIcon,        0, 0, 0, 0);
  HudMessage(s:""; msgFlags, msgIdBackground,  0, 0, 0, 0);
  HudMessage(s:""; msgFlags, msgIdBackgroundA, 0, 0, 0, 0);
  HudMessage(s:""; msgFlags, msgIdBackgroundB, 0, 0, 0, 0);
}

function void WM_MaybeShowBackground(int backgroundMode)
{
  if (!IsShowingBackground()) return;

  switch (backgroundMode)
  {
  case BACKGROUND_LEFT:
    SetFont("wmbgleft");
    SetHudSize(600, 1, true);
    HudMessage(s:"A"; msgFlags, msgIdBackground,  0, 0.0, -1.0, 0);
    HudMessage(s:"A"; msgFlags, msgIdBackgroundA, 0, 0.0,  0.0, 0);
    HudMessage(s:"A"; msgFlags, msgIdBackgroundB, 0, 0.0,  1.0, 0);
    break;

  case BACKGROUND_TOP:
    SetFont("wmbgtop");
    SetHudSize(1, 600, true);
    HudMessage(s:"A"; msgFlags, msgIdBackground,  0, -1.0, 100.0, 0);
    HudMessage(s:"A"; msgFlags, msgIdBackgroundA, 0,  0.0, 100.0, 0);
    HudMessage(s:"A"; msgFlags, msgIdBackgroundB, 0,  1.0, 100.0, 0);
    break;

  case BACKGROUND_RIGHT:
    SetFont("wmbgrght");
    SetHudSize(600, 1, true);
    HudMessage(s:"A"; msgFlags, msgIdBackground,  0, 600.0, -1.0, 0);
    HudMessage(s:"A"; msgFlags, msgIdBackgroundA, 0, 600.0,  0.0, 0);
    HudMessage(s:"A"; msgFlags, msgIdBackgroundB, 0, 600.0,  1.0, 0);
    break;
  }

  SetHudSize(0, 0, true);
}

function int WM_ItemCount(void)
{
  int result = 0;
  int weaponCount = WM_WeaponCount();
  for (int i = 0; i < weaponCount; ++i)
  {
    result += WM_IsInInventory(i);
  }
  return result;
}

function void WM_PrintMenu(void)
{
  bool highToLow      = IsHighToLow();
  int  y              = GetYStart();
  int  weaponCount    = WM_WeaponCount();
  int  step           = WM_IfThenElse((weaponCount < 20), 0.05, 0.02);
  int  stepMultiplied = (step * StepMultiplier()) >> 16;

  bool showTags;
  bool slotOnLeft;
  bool withSlot;
  int  x;
  int  xStep;
  int  yStep;
  int  iconX;
  int  backgroundMode;

  switch (DisplayMode())
  {
  case 0:
    x              = 0.0;
    xStep          = 0.0;
    yStep          = stepMultiplied;
    showTags       = true;
    slotOnLeft     = true;
    withSlot       = false;
    iconX          = 0.08;
    backgroundMode = BACKGROUND_LEFT;
    break;

  case 1:
    x              = 0.0;
    xStep          = 0.0;
    yStep          = stepMultiplied;
    showTags       = false;
    slotOnLeft     = true;
    withSlot       = false;
    iconX          = 0.08;
    backgroundMode = BACKGROUND_LEFT;
    break;

  case 2:
    x              = 0.0;
    xStep          = stepMultiplied;
    yStep          = 0.0;
    showTags       = false;
    slotOnLeft     = true;
    withSlot       = false;
    iconX          = 0.08;
    backgroundMode = BACKGROUND_TOP;
    break;

  case 3:
    x              = 0.5;
    xStep          = 0.0;
    yStep          = stepMultiplied;
    showTags       = true;
    slotOnLeft     = true;
    withSlot       = true;
    iconX          = -0.5;
    backgroundMode = BACKGROUND_TOP;
    break;

  case 4:
    x              = 0.5;
    xStep          = 0.0;
    yStep          = stepMultiplied;
    showTags       = true;
    slotOnLeft     = false;
    withSlot       = true;
    iconX          = -0.5;
    backgroundMode = BACKGROUND_TOP;
    break;

  case 5:
    x              = 0.5;
    xStep          = 0.0;
    yStep          = stepMultiplied;
    showTags       = false;
    slotOnLeft     = true;
    withSlot       = false;
    iconX          = -0.5;
    backgroundMode = BACKGROUND_TOP;
    break;

  case 6:
    x              = 0.5 -
                     ((WM_ItemCount() - 1 + WM_CountDuplicateSlots(DoubleSpaceBetweenSlots()))
                     * stepMultiplied) / 2;
    xStep          = stepMultiplied;
    yStep          = 0.0;
    showTags       = false;
    slotOnLeft     = true;
    withSlot       = false;
    iconX          = -0.5;
    backgroundMode = BACKGROUND_TOP;
    break;

  case 7:
    x              = 1.0;
    xStep          = 0.0;
    yStep          = stepMultiplied;
    showTags       = true;
    slotOnLeft     = false;
    withSlot       = false;
    iconX          = -0.92;
    backgroundMode = BACKGROUND_RIGHT;
    break;

  case 8:
    x              = 1.0;
    xStep          = 0.0;
    yStep          = stepMultiplied;
    showTags       = false;
    slotOnLeft     = false;
    withSlot       = false;
    iconX          = -0.92;
    backgroundMode = BACKGROUND_RIGHT;
    break;

  case 9:
    x              = 1.0;
    highToLow      = !highToLow;
    xStep          = -stepMultiplied;
    yStep          = 0.0;
    showTags       = false;
    slotOnLeft     = false;
    withSlot       = false;
    iconX          = -0.92;
    backgroundMode = BACKGROUND_TOP;
    break;
  }

  WM_MaybeShowBackground(backgroundMode);
  SetFont(GetMenuFont());

  if (!showTags) // print weapon info on center
  {
    int infoX = 0.5;
    switch (DisplayMode())
    {
    case 1: infoX = -0.04; break;
    case 2: infoX =  0.0;  break;
    case 5: infoX = -0.52; break;
    case 8: infoX =  0.96; break;
    case 9: infoX =  1.0;  break;
    }

    str ammo;
    if (selectedWeapon != WEAPON_HOLSTERED
        && selectedWeapon != highlightedWeapon
        && selectedWeapon != ERROR_NOT_FOUND)
    {
      str message = WM_GetPrintableWeaponName(selectedWeapon);
      if (AmmoMode() != 0)
      {
        str selectedClass = WM_GetWeaponClass(selectedWeapon);
        ammo = WM_GetAmmoString(selectedClass, AmmoMode());
        if (StrLen(ammo) != 0)
        {
          message = StrParam(s:message, s:" - ", s:ammo);
        }
      }
      HudMessage(s:" ", s:message, s:" ";
                 msgFlags, msgIdSelected,
                 SelectedWeaponColor(), infoX, y + stepMultiplied * 4, 0);
    }

    str highlightedName = WM_GetPrintableWeaponName(highlightedWeapon);
    message = highlightedName;

    if (AmmoMode() != 0)
    {
      str highlightedClass = WM_GetWeaponClass(highlightedWeapon);
      ammo = WM_GetAmmoString(highlightedClass, AmmoMode());
      if (StrLen(ammo) != 0)
      {
        message = StrParam(s:message, s:" - ", s:ammo);
      }
    }
    HudMessage(s:" ", s:message, s:" ";
               msgFlags, msgIdHighlighted,
               HighlightedColor(), infoX, y + stepMultiplied * 2, 0);

  }
  else
  {
    HudMessage(s:""; msgFlags, msgIdSelected,    0, 0, 0, 0);
    HudMessage(s:""; msgFlags, msgIdHighlighted, 0, 0, 0, 0);
  }

  int iBegin;
  int iEnd;
  int iStep;
  if (highToLow) { iBegin = weaponCount - 1; iEnd = -1;          iStep = -1; }
  else           { iBegin = 0;               iEnd = weaponCount; iStep =  1; }

  int prevSlot = -1;

  for (int i = iBegin; i != iEnd; i += iStep)
  {
    if (!WM_IsInInventory(i))
    {
      continue;
    }

    if (DoubleSpaceBetweenSlots())
    {
      int currentSlot = WM_GetWeaponSlot(i);
      bool otherSlot = (prevSlot != -1 && currentSlot != prevSlot);
      if (otherSlot)
      {
        x += xStep;
        y += yStep;
      }
      prevSlot = currentSlot;
    }

    int color;
    if      (i == highlightedWeapon) { color = HighlightedColor(); }
    else if (i == selectedWeapon)
    {
      if (!WM_HasAmmo(WM_GetWeaponClass(i))) { color = NoAmmoColor(); }
      else { color = SelectedWeaponColor(); }
    }
    else
    {
      if (!WM_HasAmmo(WM_GetWeaponClass(i))) { color = NoAmmoColor(); }
      else { color = AvailableColor(); }
    }

    WM_PrintHudWeapon(i, color, x, y, slotOnLeft, withSlot, showTags);

    x += xStep;
    y += yStep;
  }

  if (IsShowingIcons())
  {
    str weaponClass = WM_GetWeaponClass(highlightedWeapon);
    if (currentSet == SET_REKKR) { weaponClass = StrParam(s:weaponClass, s:"_rekkr"); }

    str icon = WM_GetWeaponIcon(weaponClass);
    if (StrLen(icon) > 0)
    {
      int iconY = -(GetYStart() - stepMultiplied * 2);
      SetFont(icon);
      HudMessage(s:"A"; msgFlags, msgIdIcon, 0, iconX, iconY, 0);
      SetFont(GetMenuFont());
    }
  }
}

function int WM_CountDuplicateSlots(bool doubleSpaceSlots)
{
  if (!doubleSpaceSlots) return 0;

  int count = 0;
  int prevSlot = -1;
  int weaponCount = WM_WeaponCount();

  for (int i = 0; i < weaponCount; ++i)
  {
    if (!WM_IsInInventory(i))
    {
      continue;
    }

    int currentSlot = WM_GetWeaponSlot(i);
    bool otherSlot = (prevSlot != -1 && currentSlot != prevSlot);
    if (otherSlot)
    {
      ++count;
    }
    prevSlot = currentSlot;
  }

  return count;
}

function str WM_MakeWeaponString(int weaponNumber, bool withSlot, bool slotOnLeft)
{
  str result = WM_GetPrintableWeaponName(weaponNumber);

  if (withSlot)
  {
    int slot = WM_GetWeaponSlot(weaponNumber);
    if (slotOnLeft) { result = StrParam(d:slot,   s:" ", s:result); }
    else            { result = StrParam(s:result, s:" ", d:slot  ); }
  }
  else // no slot, just space
  {
    if (slotOnLeft) { result = StrParam(s:"    ", s:result); }
    else            { result = StrParam(s:result, s:"    "); }
  }

  if (AmmoMode())
  {
    str ammo = WM_GetAmmoString(WM_GetWeaponClass(weaponNumber), AmmoMode());
    if (StrLen(ammo) != 0)
    {
      if (slotOnLeft) { result = StrParam(s:result, s:" - ", s:ammo  ); }
      else            { result = StrParam(s:ammo,   s:" - ", s:result); }
    }
  }

  return result;
}

function void WM_PrintHudWeapon( int  weaponNumber
                               , int  color
                               , int  x
                               , int  y
                               , bool slotOnLeft
                               , bool withSlot
                               , bool showTags
                               )
{
  int id = msgIdWeapon + weaponNumber;

  if (!withSlot)
  {
    int slotNumber = WM_GetWeaponSlot(weaponNumber);
    HudMessage(s:" ", i:slotNumber, s:" ";
               msgFlags, id + MAX_WEAPONS_NUMBER, color, x, y, 0);
  }

  if (!showTags) return;

  str weaponString = WM_MakeWeaponString(weaponNumber, withSlot, slotOnLeft);

  HudMessage(s:" ", s:weaponString, s:" "; msgFlags, id, color, x, y, 0);
}

// Control functions section ///////////////////////////////////////////////////

function int WM_FindRealSelectedWeapon(void)
{
  return WM_FindInWeaponList(GetWeapon());
}

function int WM_FindInWeaponList(str weaponName)
{
  int weaponCount = WM_WeaponCount();
  for (int i = 0; i < weaponCount; ++i)
  {
    if (StrICmp(weaponName, WM_GetWeaponClass(i)) == 0) return i;
  }
  return ERROR_NOT_FOUND;
}

function void WM_SelectWeapon(int weaponNumber, int playerN)
{
  if (weaponNumber == selectedWeapon || weaponNumber == -1) return;

  str  weaponName = WM_GetWeaponClass(weaponNumber);
  bool success    = SetWeapon(weaponName);

  if (success)
  {
    WM_SetSelectedWeapon(weaponNumber, playerN);
  }
  else
  {
    str printableWeaponName = WM_GetPrintableWeaponName(weaponNumber);
    HudMessage(s:printableWeaponName, s:": no ammo.";
               msgFlags, msgIdAmmo, CR_RED, 0.5, 0.5, 1.0);

    WM_SetHighlightedWeapon(selectedWeapon);
  }
}

function void WM_InitApiCvars(int playerN)
{
  int weaponCount = WM_WeaponCount();
  SetUserCVar(playerN, "M8fWeaponMenuWeaponCount", weaponCount);

  SetUserCVar      (playerN, "m8f_wm_PreviousWeaponSet",   currentSet);
  SetUserCVarString(playerN, "m8f_wm_PreviousPlayerClass", GetActorClass(0));

  for (int i = 0; i < weaponCount; ++i)
  {
    str weaponClassVarName = StrParam(s:"M8fWeaponMenuWeaponClass", i:i);
    str weaponNameVarName  = StrParam(s:"M8fWeaponMenuWeaponName",  i:i);
    str weaponSlotVarName  = StrParam(s:"M8fWeaponMenuWeaponSlot",  i:i);

    SetUserCVarString(playerN, weaponClassVarName, WM_Wrap(WM_GetWeaponClass(i)));
    SetUserCVarString(playerN, weaponNameVarName,  WM_Wrap(WM_GetPrintableWeaponName(i)));
    SetUserCVar      (playerN, weaponSlotVarName,  WM_GetWeaponSlot(i));
  }
}

function void WM_PlaySwitchSound(void)
{
  PlaySound(0, SwitchSound(), CHAN_AUTO);
}

function int WM_WeaponCount(void)
{
  return weaponCounts[currentSet];
}

// Scripts section /////////////////////////////////////////////////////////////

// Temporaryly global variables;
int oldInstant;

script "InitWeaponMenu" (int weaponSet)
{
  currentSet = weaponSet;

  if (currentSet == SET_FALLBACK)
  {
    WM_GetFallbackWeapons();
  }

  int playerN = PlayerNumber();

  WM_MaybeLoadCustomWeaponOrder(playerN);
  WM_SetSelectedWeapon(WM_FindRealSelectedWeapon(), playerN);
  WM_SetHighlightedWeapon(selectedWeapon);
  WM_UpdateIsInInventory();
  WM_InitApiCvars(playerN);
  WM_FillInventoryItems();

  if (IsAlwaysShowing()) { WM_ChangeState(STATE_CLOSED_VISIBLE, playerN); }
  else                   { WM_ChangeState(STATE_CLOSED,         playerN); }

  oldInstant = IsInstantSwitch();
}

script "ShowWeaponMenu" (void)
{
  bool instant = IsInstantSwitch();
  if (oldInstant != instant)
  {
    WM_InstantSwitch(instant == 2);
    oldInstant = instant;
  }

  if (IsVisible())
  {
    bool updated = WM_UpdateIsInInventory();
    if (updated || AmmoMode()) { WM_UpdateMenu(); }
  }

  if (IsInventory())
  {
    WM_PrintInventoryMenu(PlayerNumber());
  }

  // Check if currently selected weapon corresponds to what player is holding.
  // Handles situation when player drops a weapon, or switches to new weapon
  // on pickup.
  if (selectedWatchEnabled && !WM_IsSelected(selectedWeapon))
  {
    str currentWeaponClass = GetWeapon();
    int currentWeaponI     = WM_FindInWeaponList(currentWeaponClass);

    if (currentWeaponI != ERROR_NOT_FOUND)
    {
      WM_SetHighlightedWeapon(currentWeaponI);
      WM_SetSelectedWeapon(highlightedWeapon, PlayerNumber());
      WM_UpdateMenu();
    }
  }
}

script "ToggleWeaponMenu" (int closeWithoutAction)
{
  if (IsInventory())
  {
    int playerN = PlayerNumber();

    if (closeWithoutAction)
    {
      WM_ChangeState(STATE_CLOSED, playerN);
    }
    else
    {
      str selectedInventory = WM_GetSelectedInventory();
      if (StrCmp(selectedInventory, "wm_none") != 0)
      {
        UseInventory(selectedInventory);
        if (IsClosingAfterUse()) { WM_ChangeState(STATE_CLOSED, playerN); }
      }
      else
      {
        WM_ChangeState(STATE_CLOSED, playerN);
      }
    }
    terminate;
  }

  if (IsOpened() && closeWithoutAction)
  {
    WM_SetHighlightedWeapon(WM_FindRealSelectedWeapon());
  }

  WM_ToggleWeaponMenu(playerN);
}

function void WM_ToggleWeaponMenu(int playerN)
{
  if (IsOpened())
  {
    WM_ChangeState(STATE_CLOSED, playerN);
  }
  else
  {
    WM_ChangeState(STATE_OPENED, playerN);
  }
}

function int WM_FindSelectedInventoryIndex(void)
{
  // find currently selected
  str selectedInventoryClass = WM_GetSelectedInventory();
  for (int i = 0; i < nInventoryItems; ++i)
  {
    if (StrCmp(selectedInventoryClass, inventoryClasses[i]) == 0) return i;
  }
  return ERROR_NOT_FOUND;
}

function int WM_FindNextAvailableInventory(int selectedInventoryIndex)
{
  for (int i = 1; i < nInventoryItems; ++i)
  {
    int availableIndex = (selectedInventoryIndex + i) % nInventoryItems;

    if (!IsCycleEnabled() && availableIndex == 0) return ERROR_NOT_FOUND;
    if (CheckInventory(inventoryClasses[availableIndex])) return availableIndex;
  }
  return ERROR_NOT_FOUND;
}

function int WM_FindPrevAvailableInventory(int selectedInventoryIndex)
{
  for (int i = 1; i < nInventoryItems; ++i)
  {
    int availableIndex = (selectedInventoryIndex + nInventoryItems - i) % nInventoryItems;

    if (!IsCycleEnabled() && availableIndex == nInventoryItems - 1) return ERROR_NOT_FOUND;
    if (CheckInventory(inventoryClasses[availableIndex])) return availableIndex;
  }
  return ERROR_NOT_FOUND;
}

function void WM_SetSelectedInventory(int selectedIndex, int availableIndex)
{
  if (selectedIndex == availableIndex || availableIndex == ERROR_NOT_FOUND) return;

  WM_PlaySwitchSound();
  str inventoryClass = inventoryClasses[availableIndex];
  ScriptCall("WMZScriptHelper", "SetSelectedInventory", inventoryClass);
}

function void WM_SelectNextInventory(void)
{
  int selectedInventoryIndex = WM_FindSelectedInventoryIndex();
  if (selectedInventoryIndex == ERROR_NOT_FOUND) return;

  int nextAvailableInventoryIndex = WM_FindNextAvailableInventory(selectedInventoryIndex);
  WM_SetSelectedInventory(selectedInventoryIndex, nextAvailableInventoryIndex);
}

function void WM_SelectPreviousInventory(void)
{
  int selectedInventoryIndex = WM_FindSelectedInventoryIndex();
  if (selectedInventoryIndex == ERROR_NOT_FOUND) return;

  int prevAvailableInventoryIndex = WM_FindPrevAvailableInventory(selectedInventoryIndex);
  WM_SetSelectedInventory(selectedInventoryIndex, prevAvailableInventoryIndex);
}

script "ScrollNextWeapon" (void)
{
  int playerN = PlayerNumber();

  if (IsInventory())
  {
    WM_SelectNextInventory();
    WM_PrintInventoryMenu(playerN);
    terminate;
  }

  if (IsOpeningOnScroll() && !IsOpened())
  {
    WM_ChangeState(STATE_OPENED, playerN);
  }

  int start;
  int n;
  int i;
  int weaponCount = WM_WeaponCount();

  if (IsOpened())
  {
    start = highlightedWeapon + 1;
    for (n = 0; n < weaponCount; ++n)
    {
      i = (start + n) % weaponCount;

      if (!IsCycleEnabled() && i == 0) { terminate; }

      if (WM_IsInInventory(i) && !WM_SkipNoAmmo(i)) { break; }
    }

    WM_SetHighlightedWeapon(i);
    if (n != weaponCount - 1) { WM_PlaySwitchSound(); }
  }

  else
  {
    if (selectedWeapon == WEAPON_HOLSTERED) { start = 0;                  }
    else                                    { start = selectedWeapon + 1; }

    for (n = 0; n < weaponCount; ++n)
    {
      i = (start + n) % weaponCount;
      if (SetWeapon(WM_GetWeaponClass(i)) && !WM_SkipNoAmmo(i)) { break; }
    }

    if (n != weaponCount - 1)
    {
      WM_SetSelectedWeapon(i, playerN);
      WM_SetHighlightedWeapon(i);
      WM_MaybeDisplayNameTag();
    }
  }

  WM_UpdateMenu();
}

function void WM_MaybeDisplayNameTag(void)
{
  if (IsShowingNameTags())
  {
    str selectedName = WM_GetPrintableWeaponName(selectedWeapon);
    HudMessage(s:selectedName;
               HUDMSG_FADEOUT, msgIdTag, NameTagColor(), 0.5, 0.9, 2.0, 1.0);

  }
}

script "ScrollPreviousWeapon" (void)
{
  int playerN = PlayerNumber();

  if (IsInventory())
  {
    WM_SelectPreviousInventory();
    WM_PrintInventoryMenu(playerN);
    terminate;
  }

  if (IsOpeningOnScroll() && !IsOpened())
  {
    WM_ToggleWeaponMenu(playerN);
  }

  int start;
  int n;
  int i;
  int weaponCount = WM_WeaponCount();

  if (IsOpened())
  {
    start = highlightedWeapon - 1;
    for (n = 0; n < weaponCount; ++n)
    {
      i = (start - n + weaponCount) % weaponCount;

      if (!IsCycleEnabled() && i == weaponCount - 1) { terminate; }

      if (WM_IsInInventory(i) && !WM_SkipNoAmmo(i)) { break; }
    }

    WM_SetHighlightedWeapon(i);
    if (n != weaponCount - 1) { WM_PlaySwitchSound(); }
  }

  else
  {
    if (selectedWeapon == WEAPON_HOLSTERED) { start = weaponCount    - 1; }
    else                                    { start = selectedWeapon - 1; }

    for (n = 0; n < weaponCount; ++n)
    {
      i = (start - n + weaponCount) % weaponCount;
      if (SetWeapon(WM_GetWeaponClass(i)) && !WM_SkipNoAmmo(i)) { break; }
    }

    if (n != weaponCount - 1)
    {
      WM_SetSelectedWeapon(i, playerN);
      WM_SetHighlightedWeapon(i);
      WM_MaybeDisplayNameTag();
    }
  }

  WM_UpdateMenu();
}

function bool WM_SkipNoAmmo(int weaponIndex)
{
  if (!IsSkippingNoAmmo()) return false;

  bool hasAmmo = WM_HasAmmo(WM_GetWeaponClass(weaponIndex));
  return !hasAmmo;
}

function int WM_GetNextWeaponInSlot(int weaponIndex, bool cycle)
{
  int i       = weaponIndex;
  int stopper = WM_WeaponCount();

  do
  {
    i = WM_GetWeaponSibling(i, cycle);
    if (i == -1) { break; }
    --stopper;
  }
  while (!WM_IsInInventory(i) && stopper);

  return i;
}

script "SmartWeaponSelection" (int slotNumber)
{
  int playerN = PlayerNumber();

  if (IsInventory())
  {
    WM_ChangeState(STATE_CLOSED, playerN);
  }

  if (IsOpened())
  {
    int  currentSlot = WM_GetWeaponSlot(highlightedWeapon);
    bool sameSlot    = (slotNumber == currentSlot);
    if (sameSlot)
    {
      int sibling = WM_GetNextWeaponInSlot(highlightedWeapon, true);

      if (sibling != -1 && sibling != highlightedWeapon)
      {
        WM_SetHighlightedWeapon(sibling);
        WM_PlaySwitchSound();
      }
    }
    else
    {
      WM_HighlightFirstInInventory(slotNumber);
    }
  }

  else
  {
    if (IsSelectingIfOnlyInSlot())
    {
      int weaponNumber = WM_GetWeaponBySlotInInv(slotNumber);
      if (weaponNumber == -1) { terminate; }

      if (WM_IsOnlyInSlot(weaponNumber))
      {
        if (CheckInventory(WM_GetWeaponClass(weaponNumber)))
        {
          WM_SelectWeapon(weaponNumber, playerN);
          terminate;
        }
      }
    }

    WM_ToggleWeaponMenu(playerN);
    WM_HighlightFirstInInventory(slotNumber);
  }

  WM_UpdateMenu();
}

// Utility functions section ///////////////////////////////////////////////////

function bool WM_UpdateIsInInventory(void)
{
  bool updated     = false;
  int  weaponCount = WM_WeaponCount();

  for (int i = 0; i < weaponCount; ++i)
  {
    bool old         = isInInventory[i];
    str  weaponClass = WM_GetWeaponClass(i);

    if (CheckClass(weaponClass)) { isInInventory[i] = CheckInventory(weaponClass); }
    else                         { isInInventory[i] = false;                       }

    if (old != isInInventory[i]) { updated = true; }
  }

  return updated;
}

function void WM_ResetIsInInventory(void)
{
  int weaponCount = WM_WeaponCount();
  for (int i = 0; i < weaponCount; ++i)
  {
    isInInventory[i] = CheckInventory(WM_GetWeaponClass(i));
  }
}

function bool WM_IsInInventory(int weaponNumber)
{
  return isInInventory[weaponNumber];
}

function bool WM_IsSelected(int weaponNumber)
{
  if (weaponNumber == WEAPON_HOLSTERED) return true;
  return CheckWeapon(WM_GetWeaponClass(weaponNumber));
}

function void WM_HighlightFirstInInventory(int slotNumber)
{
  int weaponNumber = WM_GetWeaponBySlotInInv(slotNumber);
  if (WM_IsInInventory(weaponNumber) && highlightedWeapon != weaponNumber)
  {
    WM_SetHighlightedWeapon(weaponNumber);
    WM_PlaySwitchSound();
  }
}

// Global variables write access functions /////////////////////////////////////

function void WM_SetHighlightedWeapon(int weaponNumber)
{
  if (weaponNumber == WEAPON_HOLSTERED) return;
  highlightedWeapon = weaponNumber;
}

function void WM_SetSelectedWeapon(int weaponNumber, int playerN)
{
  selectedWeapon = weaponNumber;

  SetUserCVarString(playerN, "m8f_wm_SelectedWeaponTag",
                    WM_GetPrintableWeaponName(selectedWeapon));
}

function void WM_SetSelectedWatchEnabled(bool isEnabled)
{
  selectedWatchEnabled = isEnabled;
}

// Global variables read access functions //////////////////////////////////////

function int WM_GetSelectedWeapon(void)
{
  return selectedWeapon;
}

// Weapon Priority Section /////////////////////////////////////////////////////

script "WM_ApplyTagChange" (void)
{
  int playerN           = PlayerNumber();
  str newName           = GetUserCVarString(playerN, "m8f_wm_SelectedWeaponTag");
  str weaponNameVarName = StrParam(s:"M8fWeaponMenuWeaponName", i:selectedWeapon);

  SetUserCVarString(playerN, weaponNameVarName, WM_Wrap(newName));
  tags[currentSet][selectedWeapon] = newName;
}

script "WM_IncreasePriority" (void)
{
  str name = WM_GetPrintableWeaponName(highlightedWeapon);
  if (highlightedWeapon == 0)
  {
    Log(s:"Priority of ", s:name, s:" is already maximal.");
    terminate;
  }

  int oldMajorWeapon = highlightedWeapon - 1;
  int thisWeaponSlot = WM_GetWeaponSlot(highlightedWeapon);
  int oldWeaponSlot  = WM_GetWeaponSlot(oldMajorWeapon);
  if (thisWeaponSlot != oldWeaponSlot)
  {
    Log(s:"Priority of ", s:name, s:" is already maximal.");
    terminate;
  }

  int playerN = PlayerNumber();

  WM_SwapWeapons(highlightedWeapon, oldMajorWeapon, true, playerN);
  WM_SetHighlightedWeapon(oldMajorWeapon);
  Log(s:"Priority of ", s:name, s:" is increased.");
  WM_UpdateMenu();
}

function void WM_SwapWeapons(int w1, int w2, bool setCVars, int playerN)
{
  if (w1 == w2) return;

  int namesTmp = names[currentSet][w1];
  names[currentSet][w1] = names[currentSet][w2];
  names[currentSet][w2] = namesTmp;

  int tagTmp = tags[currentSet][w1];
  tags[currentSet][w1] = tags[currentSet][w2];
  tags[currentSet][w2] = tagTmp;

  int slotTmp = slots[currentSet][w1];
  slots[currentSet][w1] = slots[currentSet][w2];
  slots[currentSet][w2] = slotTmp;

  if (setCVars)
  {
    str weaponClassVarName1 = StrParam(s:"M8fWeaponMenuWeaponClass", i:w1);
    str weaponClassVarName2 = StrParam(s:"M8fWeaponMenuWeaponClass", i:w2);
    SetUserCVarString(playerN, weaponClassVarName1, WM_Wrap(WM_GetWeaponClass(w1)));
    SetUserCVarString(playerN, weaponClassVarName2, WM_Wrap(WM_GetWeaponClass(w2)));

    str weaponNameVarName1 = StrParam(s:"M8fWeaponMenuWeaponName", i:w1);
    str weaponNameVarName2 = StrParam(s:"M8fWeaponMenuWeaponName", i:w2);
    SetUserCVarString(playerN, weaponNameVarName1, WM_Wrap(WM_GetPrintableWeaponName(w1)));
    SetUserCVarString(playerN, weaponNameVarName2, WM_Wrap(WM_GetPrintableWeaponName(w2)));

    str slotVarName1 = StrParam(s:"M8fWeaponMenuWeaponSlot", i:w1);
    str slotVarName2 = StrParam(s:"M8fWeaponMenuWeaponSlot", i:w2);
    SetUserCVar(playerN, slotVarName1, WM_GetWeaponSlot(w1));
    SetUserCVar(playerN, slotVarName2, WM_GetWeaponSlot(w2));
  }
}

script "WM_IncreaseSlot" (void)
{
  int currentSlot = WM_GetWeaponSlot(highlightedWeapon);
  if (currentSlot == 11) { terminate; }

  int nextSlot[11] = { 10, 2, 3, 4, 5, 6, 7, 8, 9, 0, 11 };
  int targetSlot   = nextSlot[currentSlot];
  int playerN      = PlayerNumber();
  int i            = highlightedWeapon;
  int weaponCount  = WM_WeaponCount();

  if (i != weaponCount - 1)
  {
    while ((WM_GetWeaponSlot(i + 1) == currentSlot) && (i != weaponCount - 1))
    {
      WM_SwapWeapons(i, i + 1, true, playerN);
      if (i + 1 == selectedWeapon) { WM_SetSelectedWeapon(i, playerN); }
      ++i;
    }
  }
  WM_ResetIsInInventory();

  slots[currentSet][i] = targetSlot;
  str weaponSlotVarName = StrParam(s:"M8fWeaponMenuWeaponSlot",  i:i);
  SetUserCVar(playerN, weaponSlotVarName, targetSlot);

  WM_SetHighlightedWeapon(i);
  str name = WM_GetPrintableWeaponName(i);
  Log(s:"Slot of ", s:name, s:" is changed to ", i:targetSlot, s:".");
  WM_UpdateMenu();
}

script "WM_DecreaseSlot" (void)
{
  int currentSlot = WM_GetWeaponSlot(highlightedWeapon);
  if (currentSlot == 1) { terminate; }

  int prevSlot[12] = { 9, 0, 1, 2, 3, 4, 5, 6, 7, 8, 0, 10 };
  int targetSlot   = prevSlot[currentSlot];
  int playerN      = PlayerNumber();
  int i            = highlightedWeapon;

  if (i != 0)
  {
    while (WM_GetWeaponSlot(i - 1) == currentSlot && i != 0)
    {
      WM_SwapWeapons(i, i - 1, true, playerN);
      if (i - 1 == selectedWeapon) { WM_SetSelectedWeapon(i, playerN); }
      --i;
    }
  }
  WM_ResetIsInInventory();

  slots[currentSet][i] = targetSlot;
  str weaponSlotVarName = StrParam(s:"M8fWeaponMenuWeaponSlot",  i:i);
  SetUserCVar(playerN, weaponSlotVarName, targetSlot);

  str name = WM_GetPrintableWeaponName(highlightedWeapon);
  WM_SetHighlightedWeapon(i);
  Log(s:"Slot of ", s:name, s:" is changed to ", i:targetSlot, s:".");
  WM_UpdateMenu();
}

// Inventory menu section //////////////////////////////////////////////////////

script "WM_InventoryMenu" (void)
{
  int playerN = PlayerNumber();

  if (IsInventory())
  {
    WM_ChangeState(STATE_CLOSED, playerN);
  }
  else
  {
    if (nInventoryItems != 0)
    {
      WM_ChangeState(STATE_INVENTORY, playerN);
    }
    else
    {
      Log(s:"Inventory is empty.");
    }
  }
}

function void WM_FillInventoryItems(void)
{
  str inventoryContents       = WM_GetInventoryList();
  int inventoryContentsLength = StrLen(inventoryContents);

  int current      = 0;
  str currentToken = "";
  int iArray       = 0;

  for (int i = 0; i < inventoryContentsLength && current < N_INVENTORY_ITEMS; ++i)
  {
    int character = GetChar(inventoryContents, i);
    if (character != '>')
    {
      currentToken = StrParam(s:currentToken, c:character);
    }
    else
    {
      switch (iArray)
      {
      case 0:
        inventoryClasses[current] = currentToken;
        break;
      case 1: inventoryNames[current] = currentToken; ++current; break;
      }

      iArray = (iArray + 1) % 2;
      currentToken = "";
    }
  }

  nInventoryItems = current;
}

function void WM_PrintInventoryMenu(int playerN)
{
  WM_ClearScreen();

  int xStart           = InventoryXStart();
  int backgroundMode   = xStart / 0.5;

  WM_MaybeShowBackground(backgroundMode);
  SetFont(GetMenuFont());

  bool highToLow       = IsHighToLow();
  int  yStart          = InventoryYStart();
  if (StepMultiplier() < 0.0) { highToLow = !highToLow; }

  int stepMultiplied = (0.05 * StepMultiplier()) >> 16;

  int iBegin;
  int iEnd;
  int iStep;
  if (highToLow) {iBegin = nInventoryItems-1; iEnd = -1;              iStep = -1;}
  else           {iBegin = 0;                 iEnd = nInventoryItems; iStep =  1;}

  str selectedInventoryClass = WM_GetSelectedInventory();
  int position = 0;
  for (int i = iBegin; i != iEnd; i += iStep)
  {
    int inventoryClass = inventoryClasses[i];
    int amount = CheckInventory(inventoryClass);
    if (amount == 0) { continue; }

    int color;
    if (StrCmp(inventoryClass, selectedInventoryClass) == 0)
    {
      color = HighlightedColor();
    }
    else
    {
      color = AvailableColor();
    }

    WM_PrintInventoryItem(i, position, color, xStart, yStart, stepMultiplied, amount, playerN);
    ++position;
  }

  if (position == 0)
  {
    HudMessage(s:" (Inventory is empty) ";
               msgFlags, msgIdWeapon, AvailableColor(), xStart, yStart, 0);
  }

}

function void WM_PrintInventoryItem( int i
                                   , int position
                                   , int color
                                   , int xStart
                                   , int yStart
                                   , int stepMultiplied
                                   , int amount
                                   , int playerN
                                   )
{
  int id = msgIdWeapon + position;
  int y  = yStart + position * stepMultiplied;
  int x  = xStart;

  str itemString = inventoryNames[i];
  str itemClass  = inventoryClasses[i];

  for (int n = 0; n < N_QUICK_INVENTORY_ITEMS; ++n)
  {
    if (StrCmp(itemClass, WM_GetQuickInventoryItem(n, playerN)) == 0)
    {
      itemString = StrParam(s:itemString, s:" (Q", d:n + 1, s:")");
      break;
    }
  }
  if (amount > 1) { itemString = StrParam(s:itemString, s:" (", d:amount, s:")"); }

  HudMessage(s:" ", s:itemString, s:" "; msgFlags, id, color, x, y, 0);
}

// Fallback Mode Section ///////////////////////////////////////////////////////

function void WM_GetFallbackWeapons(void)
{
  // received data format: Class>Tag>Slot> ...
  str weaponData       = WM_GetWeaponList();
  int weaponDataLength = StrLen(weaponData);
  int current          = 0;
  int iArray           = 0;
  int tokenLength      = 0;

  for (int i = 0; i < weaponDataLength && current < MAX_WEAPONS_NUMBER; i += tokenLength + 1)
  {
    tokenLength = 0;
    while(GetChar(weaponData, i + tokenLength) != '>') { ++tokenLength; }

    str currentToken = StrMid(weaponData, i, tokenLength);

    switch (iArray)
    {
    case 0: names[SET_FALLBACK][current] = currentToken; break;

    case 1: tags [SET_FALLBACK][current] = currentToken; break;

    case 2:
      slots[SET_FALLBACK][current] = WM_StrToInt(currentToken);
      ++current;
      break;
    }

    iArray = (iArray + 1) % 3;
  }

  weaponCounts[SET_FALLBACK] = current;
}
