bool returnToLast;
int  returnTo;
bool favoriteFiring = false;

script "WM_MarkWeaponAsFavorite" (void)
{
  if (IsUninitialized()) { terminate; }

  int selectedWeapon = WM_GetSelectedWeapon();
  if (selectedWeapon == WEAPON_HOLSTERED) { terminate; }

  int playerN = PlayerNumber();
  SetUserCVar(playerN, "M8fWeaponMenuFavoriteWeapon", selectedWeapon);
  Log(s:WM_GetPrintableWeaponName(selectedWeapon), s:" is set as favorite weapon.");
}

script "WM_SelectFavoriteWeapon" (void)
{
  if (IsUninitialized()) { terminate; }

  int playerN  = PlayerNumber();
  int favorite = FavoriteWeapon();
  if (CheckInventory(WM_GetWeaponClass(favorite)))
  {
    WM_SelectWeapon(favorite, playerN);
  }

  WM_UpdateMenu(playerN);
}

script "WM_FireFavoriteWeapon" (void)
{
  if (IsUninitialized()) { terminate; }

  int selectedWeapon = WM_GetSelectedWeapon();
  if (selectedWeapon == WEAPON_HOLSTERED) { terminate; }

  int favorite = FavoriteWeapon();

  favoriteFiring = true;

  returnToLast = (favorite != selectedWeapon);
  returnTo = selectedWeapon;
  WM_SetSelectedWatchEnabled(false);

  int  playerN = PlayerNumber();
  bool instant = IsInstantSwitch();

  if (instant) { WM_InstantSwitch(true); }
  WM_SelectWeapon(favorite, playerN);
  if (instant) { Delay(1); WM_InstantSwitch(false); }

  if (FavoriteWeaponFireMode() == 2) { Delay(10); } // No fire mode

  bool isReady = WM_IsWeaponReady();
  int i = 0;
  int iMax = 35 * 2; // 2 seconds
  bool alreadyFired = false;
  while (!(WM_IsSelected(favorite) && isReady))
  {
    // this is to interrupt script in case something has gone wrong.
    if (i > 0 && isReady && !WM_IsSelected(favorite))
    {
      WM_SetSelectedWatchEnabled(true);
      terminate;
    }
    ++i;

    Delay(1);
    isReady = (WM_IsWeaponReady() || i > iMax);

    if (alreadyFired == false)
    {
      int buttons = GetPlayerInput(-1, INPUT_BUTTONS);
      alreadyFired = (buttons & (BT_ATTACK|BT_ALTATTACK));
    }
  }

  if (alreadyFired) { Delay(1); }
  else
  {
    switch (FavoriteWeaponFireMode())
    {
    case 0: ScriptCall("WMZScriptHelper", "FireWeapon"   ); break;
    case 1: ScriptCall("WMZScriptHelper", "FireWeaponAlt"); break;
    case 2: break;
    }
  }

  favoriteFiring = false;
}

script "WM_FireFavoriteSwitchBack" (void)
{
  if (IsUninitialized()) { terminate; }

  while (favoriteFiring) { Delay(1); }

  bool isReady = WM_IsWeaponDeselectable();

  while (!isReady)
  {
    Delay(1);
    isReady = WM_IsWeaponDeselectable();
  }

  if (returnToLast && CheckInventory(WM_GetWeaponClass(returnTo)))
  {
    int  playerN = PlayerNumber();
    bool instant = IsInstantSwitch();

    if (instant) { WM_InstantSwitch(true); }
    WM_SelectWeapon(returnTo, playerN);
    if (instant) { Delay(1); WM_InstantSwitch(false); }
  }

  WM_SetSelectedWatchEnabled(true);
}

function void WM_InstantSwitch(bool enable)
{
  SetPlayerProperty(0, enable, PROP_INSTANTWEAPONSWITCH);
}
